/* slicepreview.fragment */

varying vec3 normal;
varying vec2 flag;
varying vec4 drawFlag;
varying vec4 smoothFlag;
varying vec3 viewDirection;

uniform vec4 color = vec4(0.8, 0.8, 0.8, 1.0);

uniform vec4 front_ambient = vec4(0.8, 0.8, 0.8, 1.0);
uniform vec4 front_diffuse = vec4(0.8, 0.8, 0.8, 1.0);
uniform vec4 back_ambient = vec4(0.3, 0.3, 0.3, 1.0);
uniform vec4 back_diffuse = vec4(0.3, 0.3, 0.3, 1.0);
uniform vec4 specular = vec4(0.2, 0.2, 0.2, 1.0);

uniform vec3 light_direction1 = vec3(1.0, 0.0, 1.0);

uniform int showType = 0;
uniform int animation = 0;

uniform vec4 clipValue;
uniform vec2 layershow;
uniform int layerstartflag_show;

uniform vec4 typecolors[18];
uniform vec4 speedcolors[13];
uniform vec4 nozzlecolors[6];

uniform int typecolorsshow[18];

uniform float specularPower = 12.0;

vec4 directLight(vec3 light_dir, vec3 fnormal, vec4 diffuse_color, vec4 core_color)
{
	float NdotL 		  = max(dot(fnormal, light_dir), 0.0);
	diffuse_color         = diffuse_color * core_color;
	vec4 total_diffuse    = NdotL * diffuse_color;
	
//	/*
	vec3 freflection      = reflect(-light_dir, fnormal);
	vec3 fViewDirection   = normalize(viewDirection);
	float RdotV           = max(0.0, dot(freflection, fViewDirection)); 
	vec4 specularColor    = specular * pow( RdotV, specularPower) * core_color;
//	*/
	
	return total_diffuse + specularColor;
}

void main( void )
{
	if(flag.x < clipValue.x || flag.x > clipValue.y)
		discard;
		
	if(flag.x == clipValue.y && (flag.y < clipValue.z || flag.y > clipValue.w))
		discard;
		
	if(flag.x < layershow.x || flag.x > layershow.y)
		discard;
		
	if(typecolorsshow[int(drawFlag.y)] == 0)
		discard;
	
	vec4 core_color = vec4(0.5, 0.5, 0.5, 1.0);
	vec3 lightDir = normalize(light_direction1);
	
	if(showType == 0)
	{
		int stype = int(drawFlag.x);
		core_color = speedcolors[stype];
	}
	else if(showType == 1)
		core_color = typecolors[int(drawFlag.y)];
	else if(showType == 2)
		core_color = nozzlecolors[int(drawFlag.z)];
		
	if(layerstartflag_show == 1 && smoothFlag.x > 0.0 && smoothFlag.x <= 0.2)
		core_color = vec4(1.0, 1.0, 1.0, 1.0);
	
	vec3 fnormal 		  =	normalize(normal);
	vec4 ambient_color 	  = front_ambient;
	vec4 diffuse_color    = front_diffuse;
	
	ambient_color 		  = ambient_color * core_color;
	
	vec4 light_color1     = directLight(lightDir, fnormal, diffuse_color, core_color);
	
	core_color = ambient_color + light_color1;
	core_color.a = color.a;
	
	if(animation > 0)
	{
		if(flag.x == clipValue.y)
		{
			core_color += vec4(0.3, 0.3, 0.3, 0.0);
		}
		else
		{
			core_color -= vec4(0.3, 0.3, 0.3, 0.0);
		}
	}
	
	gl_FragColor = core_color;
}
