#version 150 core 
out vec4 fragmentColor;

in vec3 viewDirection;
in vec3 normal;
in vec3 worldPosition;
flat in float flag;

uniform vec4 ambient = vec4(0.0, 0.0, 0.0, 1.0);
uniform vec4 diffuse = vec4(1.0, 1.0, 1.0, 1.0);
uniform vec4 specular = vec4(0.125, 0.125, 0.125, 1.0);
uniform float specularPower = 12.0;

uniform vec3 lightDirection = vec3(0.0, 0.0, 1.0);

uniform vec4 clipPlane1;
uniform vec4 clipPlane2;

uniform int checkScope = 1;
uniform vec3 minSpace;
uniform vec3 maxSpace;

uniform int cameraDir;

uniform vec4 stateColors[6];


float distance2Plane(vec3 position, vec4 plane)
{
	vec3 p = position - vec3(0.0, 0.0, plane.w);
	return dot(p, plane.xyz);
}

vec4 directLight(vec3 light_dir, vec3 fnormal, vec4 core_color, vec4 ambient_color, vec4 diffuse_color, vec4 specular_color)
{
	float NdotL 		  = max(dot(fnormal, light_dir), 0.0);
	ambient_color 	  	  = ambient_color * core_color;

	vec3 freflection      = reflect(-light_dir, fnormal);
	vec3 fViewDirection   = normalize(viewDirection);
	float RdotV           = max(0.0, dot(freflection, fViewDirection)); 
	
	diffuse_color		  = NdotL * diffuse_color * core_color;
	specular_color        = specular_color * pow( RdotV, specularPower) * core_color;
	
	return ambient_color + diffuse_color + specular_color;
}
 

void main( void )
{
	if(cameraDir == 0)
		discard;
		
	if(distance2Plane(worldPosition, clipPlane1) < 0.0)
		discard;

	if(distance2Plane(worldPosition, clipPlane2) < 0.0)
		discard;
		
    vec4 color = stateColors[int(flag)];

	vec3 fnormal 		  =	normalize(normal);
	
	vec3 lightDir = normalize(lightDirection);
	vec4 coreColor = directLight(lightDir, fnormal, color, ambient, diffuse, specular);
	
	if(checkScope > 0)
	{
		if(worldPosition.x < minSpace.x || worldPosition.y < minSpace.y || worldPosition.z < 0.1 || worldPosition.x > maxSpace.x || worldPosition.y > maxSpace.y || worldPosition.z > maxSpace.z)
		{
			coreColor *= 0.3;
			coreColor.r += 0.6;
		}
	}
	
    fragmentColor = vec4(coreColor.rgb, color.a);
}
